plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
    id 'org.jetbrains.dokka'
}

// For testing locally
// ./gradlew build publishToMavenLocal
afterEvaluate{
    publishing {
        publications {
            maven(MavenPublication) {
                from components.release
                groupId = 'com.jmilham21'
                artifactId = 'ScrollingFeed'
                version = "1.2.0"
                artifact javadocJar
                pom {
                    description = 'A description of my library'
                }
            }
        }
    }
}

task javadocJar(type: Jar, dependsOn: dokkaHtml) {
    classifier = 'javadoc'
    from "$buildDir/dokka/html"
}

android {
    namespace 'com.jmilham21.scrollingfeed'
    compileSdk 33

    defaultConfig {
        minSdk 24
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        dataBinding true
        viewBinding true
    }
}

ext.JWPlayerVersion = '4.10.1'

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'com.google.android.material:material:1.6.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    implementation "com.jwplayer:jwplayer-core:$JWPlayerVersion"
    implementation "com.jwplayer:jwplayer-common:$JWPlayerVersion"
    implementation "com.jwplayer:jwplayer-ima:$JWPlayerVersion"

    implementation 'com.google.code.gson:gson:2.8.9'
}